# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2_ttf
pkgver=2.20.2
pkgrel=1
pkgdesc="SDL2 TrueType font library (Nintendo WiiU port)."
arch=('any')
url="https://libsdl.org/projects/SDL_ttf/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'dkp-toolchain-vars')
depends=(
  'wiiu-sdl2'
  'ppc-freetype'
  'ppc-harfbuzz'
)
source=("${url}release/SDL2_ttf-${pkgver}.tar.gz")
sha256sums=(
  '9dc71ed93487521b107a2c4a9ca6bf43fb62f6bddd5c26b055e6b91418a22053'
)
groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

build() {
  cd SDL2_ttf-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  # patch out compiling showfont and glfont
  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-harfbuzz-builtin \
    --disable-freetype-builtin \
    --disable-sdltest --without-x

  make
}

package() {
  cd SDL2_ttf-$pkgver

  source /opt/devkitpro/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  # add our static lib
  echo "Requires.private: freetype2" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_ttf.pc"

  # License
  install -Dm644 "LICENSE.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}
