pkgname=wiiu-mbedtls
pkgver=2.28.0
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
makedepends=('wiiu-cmake' 'wiiu-pkg-config' 'dkp-toolchain-vars')
groups=('wiiu-portlibs')

source=( 
  "https://github.com/ARMmbed/mbedtls/archive/refs/tags/v${pkgver}.tar.gz"
  "mbedtls-${pkgver}.patch"
)

sha256sums=(
  '6519579b836ed78cc549375c7c18b111df5717e86ca0eeff4cb64b2674f424cc'
  '37f89b474a15e6fb22a6952784cf66e9559c2780ba88533adef3aae9dccf3327'
)

build() {
  cd mbedtls-$pkgver

  patch -Np1 -i $srcdir/mbedtls-${pkgver}.patch

  ./scripts/config.pl set MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY

  ./scripts/config.pl unset MBEDTLS_SELF_TEST

  source ${DEVKITPRO}/wiiuvars.sh

  ${PORTLIBS_WIIU}/bin/powerpc-eabi-cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TESTING=Off -DENABLE_PROGRAMS=Off \
    .

  make
}

package() {
  cd mbedtls-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
}
