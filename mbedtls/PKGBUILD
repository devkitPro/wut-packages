_realname=mbedtls
pkgname=wiiu-${_realname}
pkgver=2.28.3
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
makedepends=('wiiu-cmake' 'wiiu-pkg-config' 'dkp-toolchain-vars')
groups=('wiiu-portlibs')

source=( 
  "${_realname}-${pkgver}.tar.gz::https://github.com/ARMmbed/mbedtls/archive/refs/tags/v${pkgver}.tar.gz"
  "mbedtls-${pkgver}.patch"
)

sha256sums=(
  'bdf7c5bbdc338da3edad89b2885d4f8668f9a6fffeba6ec17a60333e36dade6f'
  '3dbbf088185b5cb142732538108db56fb19783a7e30199db41e67bba095c6aec'
)

build() {
  cd ${_realname}-${pkgver}

  patch -Np1 -i $srcdir/mbedtls-${pkgver}.patch

  ./scripts/config.pl set MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY

  ./scripts/config.pl unset MBEDTLS_SELF_TEST

  /opt/devkitpro/portlibs/wiiu/bin/powerpc-eabi-cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/wiiu \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TESTING=Off -DENABLE_PROGRAMS=Off \
    .

  make
}

package() {
  cd ${_realname}-${pkgver}

  source ${DEVKITPRO}/wiiuvars.sh

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
}
