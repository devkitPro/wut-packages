# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2_mixer
pkgver=2.6.3
pkgrel=2
pkgdesc="A sample multi-channel audio mixer library."
arch=('any')
url="https://libsdl.org/projects/SDL_mixer/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
groups=('wiiu-portlibs' 'wiiu-sdl-libs')
depends=(
  'wiiu-sdl2'
  'ppc-libvorbis'
  'ppc-mpg123'
  'ppc-libmodplug'
)

source=(
  "${url}release/SDL2_mixer-${pkgver}.tar.gz"
)

sha256sums=(
  '7a6ba86a478648ce617e3a5e9277181bc67f7ce9876605eea6affd4a0d6eea8f'
)

groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

build() {
  cd SDL2_mixer-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  # patch out compiling playwave and playmus
  sed 's|\$(objects)/play.*mus\$(EXE)||' -i Makefile.in

  sed 's|libSDL2_mixer.la|libSDL2_mixer.a|' -i Makefile.in


  LIBS="-lm" ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-music-cmd \
    --disable-music-ogg-shared \
    --enable-music-mod-modplug

  make
}

package() {
  cd SDL2_mixer-$pkgver

  source /opt/devkitpro/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libmodplug libmpg123 vorbisfile" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_mixer.pc"

  # License
  install -Dm644 "LICENSE.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.txt"
}
