# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2_mixer
pkgver=2.0.4
pkgrel=4
pkgdesc="A sample multi-channel audio mixer library."
arch=('any')
commit="786f515337a51f3cb5cdbfdba63ffe8ec9c6ff69"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('wiiu-portlibs' 'wiiu-sdl-libs')
depends=(
  'wiiu-sdl2'
  'ppc-libvorbis'
  'ppc-mpg123'
)
source=("https://github.com/SDL-mirror/SDL_mixer/archive/${commit}.tar.gz")
sha256sums=('52234fa476851ef4b456665a922874630c8591f5695933e689d4fc96aaf527e9')
groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

build() {
  cd SDL_mixer-$commit

  source ${DEVKITPRO}/wiiuvars.sh

  # patch out compiling playwave and playmus
  sed 's|\$(objects)/play.*mus\$(EXE)||' -i Makefile.in

  LIBS="-lm" ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-music-cmd \
    --disable-music-ogg-shared

  make
}

package() {
  cd SDL_mixer-$commit

  source /opt/devkitpro/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: vorbisidec" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_mixer.pc"

  # License
  install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}
