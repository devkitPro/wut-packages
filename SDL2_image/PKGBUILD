# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2_image
pkgver=2.6.3
pkgrel=1
pkgdesc="SDL2 image loading library"
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'dkp-toolchain-vars')
depends=(
  'wiiu-sdl2'
  'ppc-libpng'
  'ppc-libjpeg-turbo'
  'ppc-libwebp'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz")

groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg libwebp" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  install -Dm644 "LICENSE.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.txt"
}

sha256sums=('931c9be5bf1d7c8fae9b7dc157828b7eee874e23c7f24b44ba7eff6b4836312c')
