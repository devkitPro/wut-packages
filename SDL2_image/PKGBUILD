# Maintainer:  Dave Murphy <davem@devkitpro.org>

_pkgname=SDL2_image
pkgname=wiiu-sdl2_image
pkgver=2.6.3
pkgrel=1
pkgdesc="SDL2 image loading library"
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!buildflags !strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'dkp-toolchain-vars')
depends=(
  'ppc-libjpeg-turbo'
  'ppc-libpng'
  'ppc-libwebp'
  'wiiu-sdl2'
)
source=("${url}release/${_pkgname}-${pkgver}.tar.gz"
        "wiiu-${_pkgname}-${pkgver}.patch")

groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

prepare() {
  cd ${_pkgname}-$pkgver

  patch -N -p1 -i ${srcdir}/wiiu-${_pkgname}-${pkgver}.patch

  autoreconf -v
}

build() {
  cd ${_pkgname}-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --host=powerpc-eabi \
      --disable-shared \
      --enable-static \
      --disable-sdltest \
      --disable-stb-image \
      --disable-tif

  make
}

package() {
  cd ${_pkgname}-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  install -Dm644 "LICENSE.txt" -t "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}"
}

sha256sums=('931c9be5bf1d7c8fae9b7dc157828b7eee874e23c7f24b44ba7eff6b4836312c'
            'd448b2ce7dfb9395dcb998e225a0ca9b4a2263477d588cb01dc76b07c09c17b8')
