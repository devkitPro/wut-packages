pkgname=wiiu-curl
pkgver=8.7.1
pkgrel=3
pkgdesc='Library for transferring data with URLs'
arch=('any')
url='https://curl.se/'
license=('The curl license')
options=(!strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'dkp-toolchain-vars')
depends=('wut' 'ppc-zlib' 'wiiu-mbedtls')
groups=('wiiu-portlibs')

source=("${url}/download/curl-${pkgver}.tar.xz")

build() {
  cd curl-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  ./configure \
    --prefix=${PORTLIBS_PREFIX} \
    --host=powerpc-eabi \
    --disable-shared \
    --enable-static \
    --disable-ipv6 \
    --disable-unix-sockets \
    --disable-threaded-resolver \
    --disable-manual \
    --disable-pthreads \
    --disable-socketpair \
    --disable-ntlm-wb \
    --enable-websockets \
    --with-mbedtls=${PORTLIBS_PREFIX} \
    --with-ca-path=/vol/storage_mlc01/sys/title/0005001b/10054000/content/scerts

  make -C lib
}

package() {
  cd curl-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install
  make DESTDIR="$pkgdir" install-binSCRIPTS install-pkgconfigDATA

  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}

sha256sums=('6fea2aac6a4610fbd0400afb0bcddbe7258a64c63f1f68e5855ebc0c659710cd')
